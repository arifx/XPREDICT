#+ summary: We should add a summary.
#+ endpoint: "http://example.com/sparql"

PREFIX iv: <http://bio2rdf.org/irefindex_vocabulary:>
PREFIX dv: <http://bio2rdf.org/drugbank_vocabulary:>
PREFIX hv: <http://bio2rdf.org/hgnc_vocabulary:>
PREFIX kv: <http://bio2rdf.org/kegg_vocabulary:>
SELECT DISTINCT ?drugid ?geneid WHERE {
?drug <http://bio2rdf.org/openpredict_vocabulary:indication> ?disease .
{
?d a kv:Drug .
?d kv:target ?l .
?l kv:link ?t .
BIND (URI( REPLACE(str(?t),"HSA","hsa")) AS ?target) .
?target a kv:Gene .
?target kv:x-ncbigene ?ncbi .
?d kv:x-drugbank ?drug .
}
UNION
{
  ?drug a dv:Drug .
  ?drug dv:target ?target .
  ?target dv:x-hgnc ?hgnc .
  ?hgnc hv:x-ncbigene ?ncbi .
}
BIND ( STRAFTER(str(?ncbi),"http://bio2rdf.org/ncbigene:") AS ?geneid)
BIND ( STRAFTER(str(?drug),"http://bio2rdf.org/drugbank:") AS ?drugid)

}
